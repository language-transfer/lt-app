// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        minSdkVersion = 24
        compileSdkVersion = 36
        targetSdkVersion = 36
    }
    repositories {
        google()
        mavenCentral()
        // jcenter()
    }
    dependencies {
        classpath("com.android.tools.build:gradle:8.9.1")

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:2.0.21")
    }
}

allprojects {
    repositories {
        mavenLocal()
        maven {
            // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
            url("$rootDir/../node_modules/react-native/android")
        }
        maven {
            // Android JSC is installed from npm
            url("$rootDir/../node_modules/jsc-android/dist")
        }

        google()
        mavenCentral()
        // jcenter()
        maven { url 'https://www.jitpack.io' }
    }
}

// Ensure all Android library modules have a compileSdk and BuildConfig enabled under AGP 8+
subprojects { prj ->
  prj.plugins.withId("com.android.library") {
    prj.android {
      // Match your app; 35 is a quiet default with AGP 8.9 / Build Tools 35.0.0
      compileSdk 36
      buildFeatures { buildConfig true }
    }
  }
  // If a library applies Kotlin, ensure it targets JDK 17 (Hermes/RN expects this toolchain).
  prj.plugins.withId("org.jetbrains.kotlin.android") {
    prj.kotlin { jvmToolchain(17) }
  }
}

// patch android manifest namespaces
def libraryNamespaces = [
  "react-native-navigation-bar-color"        : "com.thebylito.navigationbarcolor",
]

subprojects { prj ->
  prj.plugins.withId("com.android.library") {
    def ns = libraryNamespaces[prj.name]
    if (ns) prj.android { namespace ns }
  }
}
